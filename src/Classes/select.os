Перем Консоль;
Перем Меню;
Перем КоличествоПунктовМеню;
Перем ПозМеню;
Перем ТекПоз;
Перем ОтмеченныеПозиции;

Процедура ПриСозданииОбъекта(Заголовок, МассивЗначенийВыбора=Неопределено)

	Если МассивЗначенийВыбора = Неопределено Тогда
		Меню = Новый Массив;
	Иначе
		Меню = СтрРазделить(МассивЗначенийВыбора, ",", Ложь);
	КонецЕсли;

	Консоль = Новый Консоль();
	Консоль.ВидимостьКурсора(Ложь);

	Консоль.ВывестиСтроку(Заголовок);

КонецПроцедуры

#Область ПрограмныйИнтерфейс

Процедура ДобавитьЗначениеВыбора(Значение) Экспорт
	Меню.Добавить(Значение);
КонецПроцедуры

Функция Выбрать(МультиВыбор=Истина) Экспорт

	ПередНачаломВыбора();

	// главный цикл
	Пока Истина Цикл
		Если НЕ Консоль.НажатаКлавиша Тогда
			Приостановить(100);
		Иначе
			Клавиша = Консоль.Прочитать();
			Если Клавиша = 13 Тогда // ввод
				Прервать
			ИначеЕсли Клавиша = 32 Тогда // пробел
				ИзменитьЧекбокс(МультиВыбор);
			ИначеЕсли Клавиша = 38 Тогда // вверх
				УстановитьКурсор(-1);
			ИначеЕсли Клавиша = 40 Тогда // вниз
				УстановитьКурсор(1);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	ПослеОкончанияВыбора();
	
	Если МультиВыбор Тогда
		
		// возвращаем Массив выбранных значений
		Массив = Новый Массив;
		Для Каждого КЗ Из ОтмеченныеПозиции Цикл
			Если КЗ.Значение = Истина Тогда
				Массив.Добавить(Меню[КЗ.Ключ-1]);
			КонецЕсли;
		КонецЦикла;

		Возврат Массив;

	Иначе

		// возвращаем выбранное значение
		
		ВыбранноеЗначение = Неопределено;
		
		Для Каждого КЗ Из ОтмеченныеПозиции Цикл
			Если КЗ.Значение = Истина Тогда
				ВыбранноеЗначение = Меню[КЗ.Ключ-1];
				Прервать;
			КонецЕсли;
		КонецЦикла;

		Возврат ВыбранноеЗначение;

	КонецЕсли;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Процедура ПередНачаломВыбора()

	// подготовим результат выбора 
	ОтмеченныеПозиции = Новый Соответствие();
	КоличествоПунктовМеню = Меню.Количество();
	Для инд = 1 По КоличествоПунктовМеню Цикл
		ОтмеченныеПозиции.Вставить(инд, Ложь);
	КонецЦикла;

	// вывод меню
	Шаблон = " ( ) %1";
	Для каждого ПунктМеню Из Меню Цикл
		Консоль.ВывестиСтроку(СтрШаблон(Шаблон, ПунктМеню));
	КонецЦикла;

	ПозМеню = Консоль.КурсорВерх; // позиция курсора после вывода меню
	ТекПоз = 1 + КоличествоПунктовМеню; // текущее значение
	УстановитьКурсор(-КоличествоПунктовМеню);
	
КонецПроцедуры

Процедура ПослеОкончанияВыбора()

	// сместиться в конец меню из текущей позиции
	Консоль.КурсорВерх = ПозМеню;
	Консоль.КурсорЛево = 0;
	Консоль.ВидимостьКурсора(Истина);
	
КонецПроцедуры
Процедура ИзменитьЧекбокс(МультиВыбор)

	Если МультиВыбор = Ложь Тогда
		УстановленноеЗначениеПоз = Неопределено;
		Для каждого КЗ Из ОтмеченныеПозиции Цикл
			Если КЗ.Ключ = ТекПоз Тогда 
				// позволяет снимать текущую отметку интерактивно 
				Продолжить;
			КонецЕсли;
			Если КЗ.Значение = Истина Тогда
				УстановленноеЗначениеПоз = КЗ.Ключ;
				ОтмеченныеПозиции[КЗ.Ключ] = Ложь;
				Прервать; // только одно значение установлено
			КонецЕсли;
		КонецЦикла;

		Если НЕ УстановленноеЗначениеПоз = Неопределено Тогда
			// уберем старую отметку
			Консоль.КурсорЛево = ГоризонтальнаяПозицияОтметки();
			Консоль.КурсорВерх = ВертикальнаяПозицияОтметки(УстановленноеЗначениеПоз);
			Консоль.Вывести(" ");
			Консоль.КурсорЛево = ГоризонтальнаяПозицияОтметки();
			Консоль.КурсорВерх = ВертикальнаяПозицияОтметки(ТекПоз);
		КонецЕсли;

	КонецЕсли;

	НовоеЗначение = НЕ ОтмеченныеПозиции[ТекПоз];
	ОтмеченныеПозиции[ТекПоз] = НовоеЗначение;

	Консоль.КурсорЛево = ГоризонтальнаяПозицияОтметки();
	Консоль.Вывести(?(НовоеЗначение, "*", " "));

КонецПроцедуры

Процедура УстановитьКурсор(Смещение)
	
	Если Смещение = 1 И ТекПоз = КоличествоПунктовМеню Тогда
		Возврат;
	ИначеЕсли Смещение = -1 И ТекПоз = 1 Тогда
		Возврат;
	КонецЕсли;

	ТекПоз = ТекПоз + Смещение;
	
	Консоль.КурсорЛево = 0;
	Консоль.Вывести(" ");

	Консоль.КурсорВерх = ВертикальнаяПозицияОтметки(ТекПоз);

	Консоль.КурсорЛево = 0;
	Консоль.Вывести(">");

КонецПроцедуры 

Функция ГоризонтальнаяПозицияОтметки()
	Возврат 2;
КонецФункции

Функция ВертикальнаяПозицияОтметки(НомерЗначенияВСписке)
	Возврат ПозМеню - КоличествоПунктовМеню + НомерЗначенияВСписке - 1;
КонецФункции

#КонецОбласти
