Перем Консоль;
Перем Меню;
Перем КоличествоПунктовМеню;
Перем ПозМеню;
Перем ТекПоз;
Перем ЗначенияВыбора;

Процедура ПриСозданииОбъекта(Заголовок, МассивЗначенийВыбора=Неопределено)

	Если МассивЗначенийВыбора = Неопределено Тогда
		Меню = Новый Массив;
	Иначе
		Меню = СтрРазделить(МассивЗначенийВыбора, ",", Ложь);
	КонецЕсли;

	Консоль = Новый Консоль();

	Консоль.ВывестиСтроку(Заголовок);
	
КонецПроцедуры

#Область ПрограмныйИнтерфейс

Процедура ДобавитьЗначениеВыбора(Значение) Экспорт
	Меню.Добавить(Значение);
КонецПроцедуры

Функция Выбрать() Экспорт

	ПередНачаломВыбора();

	// главный цикл
	Пока Истина Цикл
		Если НЕ Консоль.НажатаКлавиша Тогда
			Приостановить(100);
		Иначе
			Клавиша = Консоль.Прочитать();
			Если Клавиша = 13 Тогда // ввод
				Прервать
			ИначеЕсли Клавиша = 32 Тогда // пробел
				ИзменитьЧекбокс();
			ИначеЕсли Клавиша = 38 Тогда // вверх
				УстановитьКурсор(-1);
			ИначеЕсли Клавиша = 40 Тогда // вниз
				УстановитьКурсор(1);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	ПослеОкончанияВыбора();
	
	Возврат ЗначенияВыбора;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Процедура ПередНачаломВыбора()

	// подготовим результат выбора 
	ЗначенияВыбора = Новый Соответствие();
	КоличествоПунктовМеню = Меню.Количество();
	Для инд = 1 По КоличествоПунктовМеню Цикл
		ЗначенияВыбора.Вставить(инд, Ложь);
	КонецЦикла;

	// вывод меню
	Шаблон = "[ ] %1";
	Для каждого ПунктМеню Из Меню Цикл
		Консоль.ВывестиСтроку(СтрШаблон(Шаблон, ПунктМеню));
	КонецЦикла;

	// позиционируем курсов внутрь [ ]
	Консоль.КурсорЛево = 1;

	ПозМеню = Консоль.КурсорВерх; // позиция курсора после вывода меню, с учетом сдвига окна
	ТекПоз = 1 + КоличествоПунктовМеню; // текущее значение
	УстановитьКурсор(-КоличествоПунктовМеню);
	
КонецПроцедуры

Процедура ПослеОкончанияВыбора()

	// сместиться в конец меню из текущей позиции
	Консоль.КурсорВерх = ПозМеню + КоличествоПунктовМеню - ТекПоз + 1;
	Консоль.КурсорЛево = 0;
	
КонецПроцедуры
Процедура ИзменитьЧекбокс()

	НовоеЗначение = НЕ ЗначенияВыбора[ТекПоз];
	ЗначенияВыбора[ТекПоз] = НовоеЗначение;

	Консоль.Вывести(?(НовоеЗначение, "x", " "));

	Консоль.КурсорЛево = 1;

КонецПроцедуры

Процедура УстановитьКурсор(Смещение)
	
	Если Смещение = 1 И ТекПоз = КоличествоПунктовМеню Тогда
		Возврат;
	ИначеЕсли Смещение = -1 И ТекПоз = 1 Тогда
		Возврат;
	КонецЕсли;

	ТекПоз = ТекПоз + Смещение;
	ПозМеню = ПозМеню + Смещение;
	
	Консоль.КурсорВерх = ПозМеню;

КонецПроцедуры 

#КонецОбласти
